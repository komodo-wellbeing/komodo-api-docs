---
id: power-bi
title: Power BI
slug: /power-bi/
hide_title: true
---

import CodeBlock from '@theme/CodeBlock';
import IntegrationGuideHeading from '@site/src/components/IntegrationGuideHeading';
import Logo from '@site/static/img/guides/power-bi/logo.png';
import DataSource from '@site/static/img/guides/power-bi/setup-data-source.png';
import Url from '@site/static/img/guides/power-bi/setup-url.png';
import Auth from '@site/static/img/guides/power-bi/setup-auth.png';
import Table from '@site/static/img/guides/power-bi/setup-table.png';
import AdvancedEditor from '@site/static/img/guides/power-bi/setup-advanced-editor.png';

<IntegrationGuideHeading title={'Power BI'} logo={Logo} />

Power BI enables you to analyse & visualise data as well as create custom reports & interactive dashboards. Follow the
steps below to start pulling data from the Komodo Customer API into Power BI.

Power BI can be found [here](https://www.microsoft.com/en-us/power-platform/products/power-bi).

## Compatibility

The Komodo Customer API is only compatible with **Power BI Desktop**, as **Power BI Web** does not support the **Web** data
source.

## Setup

1.  In Power BI, select **Web** as a data source.

    <img src={DataSource} alt="Select data source" height={400} />

2.  Enter the URL of the API endpoint you're wanting to pull data from.

    <img src={Url} alt="Enter API URL" height={200} />

3.  Select **Basic** as the authentication method, and enter your Komodo Customer API credentials.

    <img src={Auth} alt="Enter Basic authentication credentials" height={200} />

4.  You should then see data that has been pulled from the API endpoint. Initially this will be limited to the first
    page of results - see the [Pagination](#pagination) section below for more information.

        <img src={Table} alt="Example API data" height={400} />

## Pagination

Any API endpoints returning a list of records are paginated using cursor-based pagination. This means you may have to
make multiple requests to get all of the data you're wanting.

By default, the query generated by Power BI will only fetch the first page of results. You can alter the query via the
**Advanced Editor**.

<img src={AdvancedEditor} alt="Advanced editor button" height={200} />

### Example query

Below is an example query which will fetch all pages of users. You can use this as a template for fetching other
paginated data. Just change the values in the highlighted section based on the API endpoint you're wanting to use.

```powerquery showLineNumbers {2-13}
let
    /* CHANGE THE BELOW VALUES */
    BASE_URL = "https://us.east1.api.komodowellbeing.com/rest/v1/users",
    COLUMNS = {"id", "email", "created_at", "firstname", "lastname", "role"},
    COLUMN_TYPES = {
        {"id", Int64.Type},
        {"email", type text},
        {"created_at", type datetime},
        {"firstname", type text},
        {"lastname", type text},
        {"role", type text}
    },
    /* CHANGE THE ABOVE VALUES */

    // Function for fetching a page with cursor-based pagination
    GetPage = (cursor as nullable text) =>
        let
            // Append the cursor to the URL if we have one
            url = if cursor <> null then BASE_URL & "?cursor=" & cursor else BASE_URL,
            Source = Json.Document(Web.Contents(url)),
            data = Source[data],
            // Cast to text for compatibility with GetPage
            nextCursor = if Source[cursor] <> null then Text.From(Source[cursor]) else null,
            #"Converted to Table" = Table.FromRecords({Source})
        in
            [Data = data, NextCursor = nextCursor],

    allPages = List.Generate(
    // First request with no cursor
    () => GetPage(null),
    // Repeat until we receive a null cursor
    each [NextCursor] <> null,
    // Fetch the next page
    each GetPage([NextCursor]),
    // Return the data only
    each [Data]),

    // Combine all page data into a single list
    allData = List.Combine(allPages),

    // Convert data to a table with types
    #"Converted to Table" = Table.FromList(allData, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
    #"Expanded Column1" = Table.ExpandRecordColumn(#"Converted to Table", "Column1", COLUMNS),
    #"Changed Type" = Table.TransformColumnTypes(#"Expanded Column1", COLUMN_TYPES)

in
    #"Changed Type"
```
